options:
	cmd: clear

on script load:
	set {command::{@cmd}::poziom} to 1
	set {command::{@cmd}::cooldown} to 0 seconds

	set {command::{@cmd}::opis} to "Usuwa wiadomości"
	set {command::{@cmd}::kategoria} to "Moderacyjne"
	set {command::{@cmd}::uzycie} to "clear (liczba wiadomości|osoba) [liczba wiadomości]"

function checkNumber(arg: number) :: text:
	if {_arg} is not set:
		return "To nie liczba!"
	if {_arg} > 100:
		return "Możesz usunąć maksymalnie 100 wiadomości."
	if {_arg} <= 1:
		return "Minimalna ilość wiadomości, które można usunąć to 2."

discord command {@cmd} [<text>] [<text>]:
	aliases: wyczysc
	prefixes: "%{bot::prefix}%", "%{bot::helpmention}%"
	trigger:
		if arg-1 is not set:
			KamilAPI command error "Użyj: %{bot::prefix}%%{command::{@cmd}::uzycie}%"
		if (arg-1 parsed as number) is set:
			if getUser(arg-1) is not set:
				set {_arg} to arg-1 parsed as number
				set {_check} to checkNumber({_arg})

				if {_check} is set:
					KamilAPI cmd error {_check} to event-message

				delete event-message
				wait 5 tick
				grab last {_arg} messages in event-channel

				purge the grabbed messages with event-bot

				reply with "Pomyślnie usunięto **%arg-1%** wiadomości." and store it in {_msg}
				
				deleteMessage({_msg}, 5)
				stop
		set {_u} to getUser(arg-1)
		if {_u} is not set:
			KamilAPI cmd error "Nieprawidłowy użytkownik. Użyj: %{bot::prefix}%%{command::{@cmd}::uzycie}%" to event-message

		set {_a} to arg-2 parsed as number
		if {_a} is not set:
			KamilAPI cmd error "Nieprawidłowa liczba. Użyj: %{bot::prefix}%%{command::{@cmd}::uzycie}%" to event-message
		set {_check} to checkNumber({_a})
		if {_check} is set:
			KamilAPI cmd error {_check} to event-message

		reply with "Usuwam **%{_a}%** wiadomości użytkownika %getName(arg-1)%." and store it in {_msg}
		grab last 1000 messages in event-channel
		loop grabbed messages:
			set {_l} to loop-value
			if {_l}.getMessage().getAuthor().getId() is {_u}.getId():
				add {_l} to {_msgs::*}
				add 1 to {_juz}
				if {_juz} = {_a}:
					stop loop
		purge {_msgs::*} with event-bot
		edit {_msg} to show "Usunięto **%{_a}%** wiadomości użytkownika **%getName(arg-1)%**"
		deleteMessage({_msg}, 8)