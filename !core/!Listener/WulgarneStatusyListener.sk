function removeRoles(m: member):
	set {_g} to guild with id {bot::guild}
	loop roles of {_m}:
		set {_v} to loop-value
		{_g}.removeRoleFromMember({_m}, {_v}).complete()
		add "%{_v}.getId()%" to {_id::*}
	set {_list} to getList("%{_id::*}%")
	execute "INSERT INTO `roles` (id,rola) VALUES('%unsafe {_m}.getUser().getId()%', '%unsafe {_list}%')" in {bot::sql}

	add role with id "683726062144061440" to roles of {_m} with {bot::name}

function loadRoles(m: member):
	execute "SELECT rola FROM roles WHERE id='%unsafe {_m}.getUser().getId()%'" in {bot::sql} and store result in {_r::*}
	execute "DELETE FROM roles WHERE id='%unsafe {_m}.getUser().getId()%'" in {bot::sql}

	set {_g} to guild with id {bot::guild}

	set {_roles} to "%{_r::rola::*}%"
	replace all " " with "" in {_roles}
	set {_r::*} to {_roles} split at ","
	
	set {_r} to role with id "683726062144061440"
	{_g}.removeRoleFromMember({_m}, {_r}).complete()

	loop {_r::*}:
		set {_rola} to role with id "%loop-value%"
		{_g}.addRoleToMember({_m}, {_rola}).complete()

function containsSwear(t: text) :: boolean:
	set {_kek::*} to lines from file "plugins/Skript/scripts/P2WB0T/commands/!Utils/Przeklenstwa.api"
	loop {_kek::*}:
		if loop-value is {_t}:
			return true
	return false

function containsLink(t: text) :: boolean:
	if {_t} contains "derp" or "feerko" or "roizy" or "hypixel" or "hajsmc": # za te rzeczy nie bedziemy karac
		return false

	set {_https} to regex replace all pattern "\w+:\/{2}[\d\w-]+(\.[\d\w-]+)*(?:(?:[^\s/]*))*" with "TutajJestJakisLink" in {_t}
	if {_https} contains "TutajJestJakisLink":
		return true
	set {_bezHttps} to regex replace all pattern "([0-9a-z_-]+\.)+(aero|asia|biz|cat|com|coop|edu|gov|info|int|jobs|mil|mobi|museum|name|net|org|pro|tel|travel|ac|ad|ae|af|ag|ai|al|am|an|ao|aq|ar|as|at|au|aw|ax|az|ba|bb|bd|be|bf|bg|bh|bi|bj|bm|bn|bo|br|bs|bt|bv|bw|by|bz|ca|cc|cd|cf|cg|ch|ci|ck|cl|cm|cn|co|cr|cu|cv|cx|cy|cz|cz|de|dj|dk|dm|do|dz|ec|ee|eg|er|es|et|eu|fi|fj|fk|fm|fo|fr|ga|gb|gd|ge|gf|gg|gh|gi|gl|gm|gn|gp|gq|gr|gs|gt|gu|gw|gy|hk|hm|hn|hr|ht|hu|id|ie|il|im|in|io|iq|ir|is|it|je|jm|jo|jp|ke|kg|kh|ki|km|kn|kp|kr|kw|ky|kz|la|lb|lc|li|lk|lr|ls|lt|lu|lv|ly|ma|mc|md|me|mg|mh|mk|ml|mn|mn|mo|mp|mr|ms|mt|mu|mv|mw|mx|my|mz|na|nc|ne|nf|ng|ni|nl|no|np|nr|nu|nz|nom|pa|pe|pf|pg|ph|pk|pl|pm|pn|pr|ps|pt|pw|py|qa|re|ra|rs|ru|rw|sa|sb|sc|sd|se|sg|sh|si|sj|sj|sk|sl|sm|sn|so|sr|st|su|sv|sy|sz|tc|td|tf|tg|th|tj|tk|tl|tm|tn|to|tp|tr|tt|tv|tw|tz|ua|ug|uk|us|uy|uz|va|vc|ve|vg|vi|vn|vu|wf|ws|ye|yt|yu|za|zm|zw|arpa)" with "TutajJestJakisLink" in {_t}
	if {_bezHttps} contains "TutajJestJakisLink":
		return true

	return false

function checkStatus(u: member):
	add "%game of {_u}%" to {_status::*}
	loop ...{_u}.getActivities():
		set {_v} to loop-value
		add "%{_v}.getState()%" to {_status::*}
		add "%{_v}.getDetails()%" to {_status::*}

	loop 10 times:
		remove "<none>" from {_status::*}

	loop {_status::*}:
		set {_v::1} to loop-value
		if containsSwear({_v::1}):
			removeRoles({_u})
			stop

		if containsLink({_v::1}):
			removeRoles({_u})
			stop

function checkUnstatus(u: member):
	add "%game of {_u}%" to {_status::*}
	loop ...{_u}.getActivities():
		set {_v} to loop-value
		add "%{_v}.getState()%" to {_status::*}
		add "%{_v}.getDetails()%" to {_status::*}

	loop 10 times:
		remove "<none>" from {_status::*}

	loop {_status::*}:
		set {_v::1} to loop-value
		if containsSwear({_v::1}):
			stop

		if containsLink({_v::1}):
			stop
	
	loadRoles({_u})


every 2 minutes:
	set {_g} to guild with id {bot::guild}

	loop ...{_g}.getMembers():
		set {_v} to loop-value
		if "%online status of {_v}%" != "offline":
			checkStatus({_v})

every 30 seconds:
	set {_c} to channel with id "683720945835573269"

	loop ...{_c}.getMembers():
		set {_v} to loop-value
		checkUnstatus({_v})